# $Id$
# Maintainer: Simone De Gregori <orion@runeaudio.com>

pkgname=mpd-rune
pkgver=0.18.16
pkgrel=1
pkgdesc='RuneAudio MPD optimized version'
url='http://www.musicpd.org/'
license=('GPL')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
depends=('audiofile' 'mpg123' 'curl' 'faad2' 'sqlite' 'libmms' 'libid3tag' 'libmpdclient')
#makedepends=('doxygen')
provides=("mpd=$pkgver")
conflicts=('mpd')
replaces=('mpd')
source=("http://www.musicpd.org/download/mpd/${pkgver%.*}/mpd-${pkgver}.tar.xz"
        'tmpfiles.d'
        'mpd.conf')
sha1sums=('ef510446e858fadf20d36fa2c1bed6f35a51e613'
          'f4d5922abb69abb739542d8e93f4dfd748acdad7'
          'e75a826590a38e3a18ba6b755a005eb63e650b4c')
backup=('etc/mpd.conf')
#install=install

build() {
	cd "${srcdir}/mpd-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-ao \
		--disable-pulse \
		--disable-shout \
		--disable-sidplay \
		--disable-modplug \
		--disable-soundcloud \
		--disable-wavpack \
		--disable-opus \
		--disable-lame-encoder \
		--disable-ipv6 \
		--disable-recorder-output \
		--disable-iso9660 \
		--disable-zzip \
		--disable-wildmidi \
		--disable-oss \
		--disable-fluidsynth \
		--disable-bzip2 \
		--disable-gme \
		--disable-mpg123 \
		--enable-mad=yes \
		--enable-libmpdclient \
		--enable-jack \
		--with-zeroconf=avahi \
		--with-systemdsystemunitdir=/usr/lib/systemd/system
	make
}

package() {
	cd "${srcdir}/mpd-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../mpd.conf "${pkgdir}"/etc/mpd.conf
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/mpd.conf
	install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd/playlists

	install -Dm644 "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
	sed '/WantedBy=/c WantedBy=default.target' -i "${pkgdir}"/usr/lib/systemd/user/mpd.service
}
